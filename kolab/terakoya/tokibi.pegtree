

NStatement = NPhrase _NPhrase?
_NPhrase = {^ (_ "|" NPhrase)+ #NChoice } 

NPhrase = { NChunk+ #NPhrase } 
NChunk = (NSuffix _NOrdered? / NTerm) _
_NOrdered = {^ (_ '/' _  NSuffix )+ #NOrdered }

NSuffix = 
  / &THEN '{' NStatement '}'
  / { NTerm (!SUFFIX NTerm)* { SUFFIX _VER? } #NSuffix }
  / "[" { (!(SUFFIX '|'? ']') .)+  SUFFIX '|'? #NWord } "]"


THEN = '{' (BRACE / !(. '}') .)+ [でて、] ']'? '}'
BRACE = '{' (BRACE / !'}' .)* '}' 
//NOUN = BRACE SUFFIX

SUFFIX = 
  / 'として' _OF
  / 'を' / 'で' _OF / 'に' / 'が' / 'は' / 'も' / 'の'
  / 'と' _OF / 'から' _OF / 'まで' _OF / '、'

_OF = 'の'?
_VER = '@' [A-Za-z0-9]+

example NPhrase 望遠鏡で子犬が泳ぐ様子を見る
example NPhrase 望遠鏡で/{子犬が泳ぐ}様子を見る
example NPhrase {望遠鏡を用いて}/{子犬が泳ぐ}様子を見る
example NPhrase {心が折れた|やる気が失せた}かも知れない
example NPhrase {xを{functionを適用して}フィルタした}リスト
example NPhrase {{それぞれのx}を/{functionに適用して}フィルタした}リスト
example NPhrase {それぞれのx|xのそれぞれ}をフィルタする
example NPhrase aを/[降順に]ソートする

NTerm = 
  / '{' NStatement '}' {^ NWord #NClause }?
  / NSymbol _NParam?
  / NLiteral _NParam?
  / ![ }/|] NWord
  / ![ }/|] NPiece

NSymbol = 
  / { [A-Za-z_] [A-Za-z_0-9]* #NSymbol }
  / "(" { (!")" . )+ #NSymbol } ")"
_NParam = "(" {^ { (!")" . )+ #NWord } #NParam } ")"

NLiteral = 
  / {'"' (ESC / !'"' .)* '"' #NLiteral } _
  / {'\'' (ESC / !'\'' .)* '\'' #NLiteral } _
  / {'-'? [0-9]+ ('.' [0-9]+)* #NLiteral } _

ESC = '\\' .

NWord = 
  / "[" { (!']' .)+  #NWord } "]"
  / { RESERVED_NOUN #NWord }
  / { (!SUFFIX ![ /|[{}`] .)+ #NWord }

RESERVED_NOUN =
  / 'とき' / 'ところ' / 'こと' / 'よう'

NPiece = 
    / '`' { (ESC / !'`' .)*  #NPiece } '`' _
    / { (![A-Za-z0-9_ "'`\[(){}/|] !'->' . )+ #NPiece }

HIRA = [ぁ-ん]
KATA = [ァ-ヶー・]
KANJI = [㐀-䶵一-龠々〇〻]
